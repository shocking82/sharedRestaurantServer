buildscript {
    ext {
        springBootVersion = '2.0.3.RELEASE'
    }

    ext.databaseLibrary = 'org.mariadb.jdbc:mariadb-java-client:2.2.4'
    ext.databaseUrl = 'jdbc:mariadb://sarang628.iptime.org:3306/banana'
    ext.databaseUsername = 'banana'
    ext.databasePassword = 'banana!@#'

    repositories {
        mavenCentral()
        maven{
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath('nu.studer:gradle-jooq-plugin:2.0.11')
        classpath('org.flywaydb:flyway-gradle-plugin:5.1.3')
    }
}

subprojects{
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    group = 'com.johnsong.android'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = 1.8

    repositories {
        mavenCentral()
    }

    dependencies {
        //Logger
        compile('org.springframework.boot:spring-boot-starter-logging')
        compile('org.springframework.boot:spring-boot-starter-log4j2')
    }

    task initSourceFolders {
        sourceSets*.java.srcDirs*.each {
            if( !it.exists() ) {
                it.mkdirs()
            }
        }

        sourceSets*.resources.srcDirs*.each {
            if( !it.exists() ) {
                it.mkdirs()
            }
        }
    }

}

project(':springbootpjt'){
    bootJar{
        baseName = 'gs-messaging-rabbitmq'
        version = '0.1.0'
    }

    dependencies {
        compile('org.springframework.boot:spring-boot-starter-data-redis')
        compile('org.springframework.boot:spring-boot-starter-data-rest')
        compile('org.springframework.boot:spring-boot-starter-hateoas')
        compile('org.springframework.boot:spring-boot-starter-web')
        compile('org.springframework.boot:spring-boot-starter-test')
        compile('org.springframework.boot:spring-boot-starter-thymeleaf')
        compile('org.springframework.boot:spring-boot-starter-amqp')
        compile group:'io.springfox',name:'springfox-swagger2',version:'2.2.2'
        compile group:'io.springfox',name:'springfox-swagger-ui',version:'2.2.2'
        compile project(':jooqflywaypjt')

    }
}

project(':strategypjt'){
    dependencies{
        compile('org.jooq:jooq-codegen:3.10.8')
    }
}

project(':jooqflywaypjt'){
    apply plugin: 'nu.studer.jooq'
    apply plugin: 'org.flywaydb.flyway'
//    plugin{
//        dependencies{
//            jooqRuntime project(':strategypjt')
//        }
//    }

    dependencies{
        compile('org.springframework.boot:spring-boot-starter-jooq')
        compile('org.flywaydb:flyway-core')
        compile databaseLibrary
        jooqRuntime databaseLibrary
        jooqRuntime project(':strategypjt')
    }

    flyway {
        url = databaseUrl
        user = databaseUsername
        password = databasePassword
        schemas = ['banana']
    }

    jooq {
        version = '3.10.0'
        edition = 'OSS'
        banana(sourceSets.main){
            jdbc {
                driver = 'org.mariadb.jdbc.Driver'
                url = databaseUrl
                user = databaseUsername
                password = databasePassword
            }
            generator {
                name = 'org.jooq.util.DefaultGenerator'
                strategy {
                    name = 'org.jooq.util.DefaultGeneratorStrategy'
//                    prefix = 'J'
                }
//                strategy {
//                    name = 'com.johnsong.android.strategy.JPrefixJooqStrategyConfig'
//                }
                database {
                    name = 'org.jooq.util.mariadb.MariaDBDatabase'
                    inputSchema = 'banana'
                    outputSchema = 'banana'
                }
                generate {
                    relations = true
                    deprecated = false
                    records = false
                    immutablePojos = true
//                    pojos = true
//                    daos= true
                    fluentSetters = true
                }
                target {
                    packageName = 'com.johnsong.android.jooq'
                    directory = 'src/main/java'
                }
            }
        }
    }

    generateBananaJooqSchemaSource.dependsOn flywayMigrate

}