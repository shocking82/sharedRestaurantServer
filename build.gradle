buildscript {
    ext {
        springBootVersion = '2.0.3.RELEASE'
    }

    ext.databaseLibrary = 'org.mariadb.jdbc:mariadb-java-client:2.2.4'
    ext.databaseUrl = 'jdbc:mariadb://sarang628.iptime.org:3306/banana'
    ext.databaseUsername = 'banana'
    ext.databasePassword = 'banana!@#'

    repositories {
        mavenCentral()
        maven{
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath('nu.studer:gradle-jooq-plugin:2.0.11')
        classpath('org.flywaydb:flyway-gradle-plugin:5.1.3')
    }
}

apply plugin: 'nu.studer.jooq'
apply plugin: 'org.flywaydb.flyway'
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group = 'com.johnsong.android'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

bootJar{
    baseName = 'gs-messaging-rabbitmq'
    version = '0.1.0'
}


ext {
    springCloudVersion = 'Finchley.RELEASE'
}

dependencies {
    compile('org.springframework.boot:spring-boot-starter-aop')
    //compile('org.springframework.boot:spring-boot-starter-data-mongodb')
    //compile('org.springframework.boot:spring-boot-starter-data-mongodb-reactive')
    compile('org.springframework.boot:spring-boot-starter-data-redis')
    compile('org.springframework.boot:spring-boot-starter-data-rest')
    compile('org.springframework.boot:spring-boot-starter-hateoas')
    compile('org.springframework.boot:spring-boot-starter-jooq')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-starter-webflux')
    compile('org.flywaydb:flyway-core')
    //compile('org.mybatis.spring.boot:mybatis-spring-boot-starter:1.3.2')
    compile('org.springframework.cloud:spring-cloud-starter')
    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('io.projectreactor:reactor-test')
    testCompile('org.springframework.restdocs:spring-restdocs-mockmvc')
    compile('org.springframework.boot:spring-boot-starter-thymeleaf')
    compile databaseLibrary
    jooqRuntime databaseLibrary
    compile('org.jooq:jooq')
    compile('org.springframework.boot:spring-boot-starter-amqp')
    compile group:'io.springfox',name:'springfox-swagger2',version:'2.2.2'
    compile group:'io.springfox',name:'springfox-swagger-ui',version:'2.2.2'

}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}


flyway {
    url = databaseUrl
    user = databaseUsername
    password = databasePassword
    schemas = ['banana']
}

jooq {
    version = '3.10.0'
    edition = 'OSS'
    banana(sourceSets.main){
        jdbc {
            driver = 'org.mariadb.jdbc.Driver'
            url = databaseUrl
            user = databaseUsername
            password = databasePassword
        }
        generator {
            name = 'org.jooq.util.DefaultGenerator'
            strategy {
                name = 'org.jooq.util.DefaultGeneratorStrategy'
            }
            database {
                name = 'org.jooq.util.mariadb.MariaDBDatabase'
//                inputSchema = 'public'
            }
            generate {
                relations = true
                deprecated = false
                records = true
                immutablePojos = true
                fluentSetters = true
            }
            target {
                packageName = 'com.johnsong.android.spring.jooq'
                directory = 'src/main/java'
            }
        }
    }
}
flywayClean.doFirst {
    delete 'src/main/generate/jooq'
}
generateBananaJooqSchemaSource.dependsOn flywayMigrate